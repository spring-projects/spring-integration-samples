pluginManagement {
	repositories {
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/release' }
	}
}

plugins {
	id 'com.gradle.enterprise' version '3.16.2'
	id 'io.spring.ge.conventions' version '0.0.15'
}

rootProject.name = 'spring-integration-samples'

['advanced', 'applications', 'basic', 'intermediate', 'dsl'].each {
	def projectDir = new File(rootDir, it)
	include ":${it}"
	projectDir.eachDir { dir ->
		if (!dir.name.startsWith('.') && 'splunk' != dir.name) {
			include ":${dir.name}"
			project(":${dir.name}").projectDir = new File(projectDir.absolutePath, dir.name)
			if ('cafe' == dir.name) {
				dir.eachDir { cafe ->
					include ":${cafe.name}"
					project(":${cafe.name}").projectDir = new File(dir.absolutePath, cafe.name)
				}
			}
		}
	}
}
